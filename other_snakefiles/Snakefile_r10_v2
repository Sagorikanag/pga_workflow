### snakemake file for aligning GIAB HG002 samples (ONT Q25 reads) with the v2 graphs

#INCLUDING RULE FILES
SAMPLE_IDS = config['SAMPLE_IDS']
REGION_ID = config['region_id']
ASM_PRESET= config['MINIMAP']['asmPreset']
tmpdir = config['TMPDIR']

### ALL RULES
rule all:
	input:
		expand("results/reads/{sample_id}.fasta", sample_id=SAMPLE_IDS),
		expand("results/alignment_chm13/{sample_id}/{sample_id}_to_CHM13_minimap.bam", sample_id=SAMPLE_IDS),
		expand("results/alignment_chm13/{sample_id}/{sample_id}_to_CHM13_minimap.bam.bai", sample_id=SAMPLE_IDS),
		# expand("results/alignment_chm13/{sample_id}/{sample_id}_to_CHM13_dv.vcf.gz", sample_id=SAMPLE_IDS),		
		"results/graph/hprc-v1.1-mc-chm13.d9.pg.vg",
		expand("results2/alignment/{sample_id}/alignments-combined.processed.sorted.gaf.gz", sample_id=SAMPLE_IDS),
		expand("results2/alignment/{sample_id}/alignments-combined.processed.sorted.gaf.gz.tbi", sample_id=SAMPLE_IDS),
		expand("results2/{sample_id}/{region_id}/chunk/subgraph.pg.vg", sample_id=SAMPLE_IDS, region_id=REGION_ID),
		expand("results2/{sample_id}/{region_id}/chunk/subgraph.pg.gfa", sample_id=SAMPLE_IDS, region_id=REGION_ID),
		expand("results2/{sample_id}/{region_id}/chunk/subgraph.gaf", sample_id=SAMPLE_IDS, region_id=REGION_ID),
		expand("results2/{sample_id}/{region_id}/anchors/subgraph.pkl", sample_id=SAMPLE_IDS, region_id=REGION_ID),
		expand("results2/{sample_id}/{region_id}/anchors/subgraph.anchors.json.extended.jsonl", sample_id=SAMPLE_IDS, region_id=REGION_ID),
		expand("results2/{sample_id}/{region_id}/anchors/subgraph.anchors.json.jsonl", sample_id=SAMPLE_IDS, region_id=REGION_ID),
		expand("results2/{sample_id}/{region_id}/shasta/{sample_id}.subregion.fasta", sample_id=SAMPLE_IDS, region_id=REGION_ID),
		expand("results2/{sample_id}/{region_id}/shasta/ShastaRun/Assembly.fasta", sample_id=SAMPLE_IDS, region_id=REGION_ID),
		expand("results2/{sample_id}/{region_id}/shasta_noext/ShastaRun/Assembly.fasta", sample_id=SAMPLE_IDS, region_id=REGION_ID),
		expand("results2/{sample_id}/{region_id}/assembly_alignment/{sample_id}_{region_id}_shasta_to_hg002_minimap_{asm_preset}.paf", sample_id=SAMPLE_IDS, region_id=REGION_ID, asm_preset=ASM_PRESET),
		expand("results2/{sample_id}/{region_id}/anchors/anchor_reads_info.tsv", sample_id=SAMPLE_IDS, region_id=REGION_ID),
		expand("results2/{sample_id}/{region_id}/anchor_stats", sample_id=SAMPLE_IDS, region_id=REGION_ID),
		expand("results2/{sample_id}/{region_id}/anchors/extended_anchor_reads_info.tsv", sample_id=SAMPLE_IDS, region_id=REGION_ID),
		expand("results2/{sample_id}/{region_id}/extended_anchor_stats", sample_id=SAMPLE_IDS, region_id=REGION_ID),
		expand("results2/{sample_id}/{region_id}/assembly_alignment/hg002.chunked.fasta", sample_id=SAMPLE_IDS, region_id=REGION_ID),
		expand("results2/{sample_id}/{region_id}/assembly_alignment/{sample_id}_{region_id}_ZOOMED_shasta_to_hg002_minimap_{asm_preset}.paf", sample_id=SAMPLE_IDS, region_id=REGION_ID, asm_preset=ASM_PRESET),
		expand("results2/{sample_id}/{region_id}/assembly_alignment/{sample_id}_{region_id}_ZOOMED_shasta_to_hg002_minimap_{asm_preset}_displayPaf.csv", sample_id=SAMPLE_IDS, region_id=REGION_ID, asm_preset=ASM_PRESET),
		expand("results2/{sample_id}/{region_id}/assembly_alignment/{asm_preset}_plots", sample_id=SAMPLE_IDS, region_id=REGION_ID, asm_preset=ASM_PRESET)


rule align_ont_r10_GIAB_with_vg_giraffe:
	output:
		alignment="results2/alignment/{sample_id}/{sample_id}.gaf.zst"
	input:
		script="workflow/scripts/process_out.awk",
		graph_gbz=config["graph_base"] + ".gbz",
		distance_index=config["graph_base"] + ".dist",
		# minimizer_index=config["graph_base"] + "." + config["minparams"] + ".withzip.min",
		# zipcodes=config["graph_base"] + "." + config["minparams"] + ".zipcodes",
		minimizer_index="/private/groups/patenlab/anovak/projects/hprc/lr-giraffe/graphs/hprc-v2.0-mc-chm13-eval.d46.k31.w50.W.withzip.min",
		zipcodes="/private/groups/patenlab/anovak/projects/hprc/lr-giraffe/graphs/hprc-v2.0-mc-chm13-eval.d46.k31.w50.W.zipcodes",
		ont_r10_sequence="/private/groups/migalab/kkyriaki/experiments/data/GIAB_2025/HG002/{sample_id}/{sample_id}.fastq"
	benchmark:
		"benchmarks/v2/{sample_id}/align_ont_r10_GIAB_with_vg_giraffe.benchmark.txt"
	log:
		"logs/v2/{sample_id}/align_ont_r10_GIAB_with_vg_giraffe.log"
	threads: 128
	shell:
		"""
		vg giraffe -t {threads} --parameter-preset r10 --progress -f {input.ont_r10_sequence} -Z {input.graph_gbz} -d {input.distance_index} -m {input.minimizer_index} -z {input.zipcodes} --output-format gaf 2> {log} | {input.script} | zstd > {output.alignment}
		"""

rule parse_gaf:
	output:
		file="results2/alignment/{sample_id}/alignments-combined.processed.gaf"
	input:
		script="workflow/scripts/parse_gaf.py",
		gaf="results2/alignment/{sample_id}/{sample_id}.gaf.zst"
	log:
		"logs/v2/{sample_id}/parse_gaf.{sample_id}.log"
	shell:
		"""
		python3 {input.script} {input.gaf} > {output.file} 2> {log}
		"""

rule sort_index_gaf:
	output:
		sorted_gaf = "results2/alignment/{sample_id}/alignments-combined.processed.sorted.gaf.gz",
		gaf_index = "results2/alignment/{sample_id}/alignments-combined.processed.sorted.gaf.gz.tbi"
	input:
		combined_gaf="results2/alignment/{sample_id}/alignments-combined.processed.gaf"
	threads: 64
	shell:
		"""
		vg gamsort -t {threads} -p -G {input.combined_gaf} | bgzip -c > {output.sorted_gaf}
		tabix -@ {threads} -p gaf {output.sorted_gaf}
		"""

rule convert_fq_to_fasta:
	output:
		fasta="results/reads/{sample_id}.fasta"
	input:
		ont_r10_sequence="/private/groups/migalab/kkyriaki/experiments/data/GIAB_2025/HG002/{sample_id}/{sample_id}.fastq"
	benchmark:
		"benchmarks/{sample_id}/convert_fq_to_fasta.benchmark.txt"
	log:
		"logs/{sample_id}/convert_fq_to_fasta.log"
	shell:
		"""
		# Convert FASTQ to FASTA
		cat {input.ont_r10_sequence} | awk '{{if(NR%4==1) {{printf(">%s\\n",substr($0,2));}} else if(NR%4==2) print;}}' > {output.fasta}
		"""

rule vg_convert_graph:
	output:
		graph_pg_vg="results/graph/hprc-v2.0-mc-chm13-eval.d46.pg.vg"
	input:
		graph_gbz=config["graph_base"] + ".gbz"
	benchmark:
		"benchmarks/v2/vg_convert_graph.benchmark.txt"
	threads: 128
	shell:
		"""
		vg convert -t {threads} {input.graph_gbz} -p > {output.graph_pg_vg}
		"""

rule vg_chunk:
	output:
		subgraph_vg="results2/{sample_id}/{region_id}/chunk/subgraph.vg",
		chunked_gaf="results2/{sample_id}/{region_id}/chunk/subgraph.gaf",
		subgraph_pg_vg="results2/{sample_id}/{region_id}/chunk/subgraph.pg.vg",
		subgraph_pg_gfa="results2/{sample_id}/{region_id}/chunk/subgraph.pg.gfa"
	input:
		sorted_gaf="results2/alignment/{sample_id}/alignments-combined.processed.sorted.gaf.gz",
		graph_gbz=config["graph_base"] + ".gbz",
		graph_snarl=config["graph_snarl"]
	params:
		region="CHM13#0#" + config['region']['chromosome'] + ":" + config['region']['start'] + "-" + config['region']['end'],
		region_underscore="_0_CHM13#0#" + config['region']['chromosome'] + "_" + config['region']['start'] + "_" + config['region']['end'],
		prefix="results2/{sample_id}/{region_id}/chunk/subgraph",
		region_id=config["region_id"]
	benchmark:
		"benchmarks/v2/{sample_id}/{region_id}/vg_chunk.benchmark.txt"
	log:
		"logs/v2/{sample_id}/{region_id}/vg_chunk.log"
	threads: 128
	shell:
		"""
        vg chunk -a {input.sorted_gaf} -F -g -x {input.graph_gbz} -p {params.region} -S {input.graph_snarl} --trace -t {threads} -b {params.prefix} > {output.subgraph_vg}
        mv {params.prefix}{params.region_underscore}.gaf {output.chunked_gaf}
		vg convert -p {output.subgraph_vg} > {output.subgraph_pg_vg}
		vg convert -f {output.subgraph_pg_vg} > {output.subgraph_pg_gfa}
		"""

rule vg_index_dist:
	output:
		subgraph_pg_dist="results2/{sample_id}/{region_id}/chunk/subgraph.pg.dist"
	input:
		subgraph_pg_vg="results2/{sample_id}/{region_id}/chunk/subgraph.pg.vg"
	log:
		"logs/v2/{sample_id}/{region_id}/vg_index_dist.log"
	threads: 32
	shell:
		"""
		 vg index {input.subgraph_pg_vg} --threads {threads} --dist-name {output.subgraph_pg_dist}
		"""

rule generate_anchors_dictionary:
	output:
		anchors_dictionary="results2/{sample_id}/{region_id}/anchors/subgraph.pkl"
	input:
		subgraph_pg_dist="results2/{sample_id}/{region_id}/chunk/subgraph.pg.dist",
		graph_pg_vg="results/graph/hprc-v2.0-mc-chm13-eval.d46.pg.vg"
	benchmark:
		"benchmarks/v2/{sample_id}/{region_id}/generate_anchors_dictionary.benchmark.txt"
	log:
		"logs/v2/{sample_id}/{region_id}/generate_anchors_dictionary.log"
	shell:
		"""
		vg_anchor build --graph {input.graph_pg_vg} --index {input.subgraph_pg_dist} --output-prefix results2/{wildcards.sample_id}/{wildcards.region_id}/anchors/subgraph > {log} 2>&1
		"""

rule get_anchors_from_gaf:
	output:
		anchors_preext="results2/{sample_id}/{region_id}/anchors/subgraph.anchors.json.jsonl",
		anchors="results2/{sample_id}/{region_id}/anchors/subgraph.anchors.json.extended.jsonl"
	input:
		anchors_dictionary="results2/{sample_id}/{region_id}/anchors/subgraph.pkl",
		chunked_gaf="results2/{sample_id}/{region_id}/chunk/subgraph.gaf",
		graph_pg_vg="results/graph/hprc-v2.0-mc-chm13-eval.d46.pg.vg"
	benchmark:
		"benchmarks/v2/{sample_id}/{region_id}/get_anchors_from_gaf.benchmark.txt"
	log:
		"logs/v2/{sample_id}/{region_id}/get_anchors_from_gaf.log"
	shell:
		"""
		vg_anchor get-anchors --dictionary {input.anchors_dictionary} --graph {input.graph_pg_vg} --alignment {input.chunked_gaf} --output results2/{wildcards.sample_id}/{wildcards.region_id}/anchors/subgraph.anchors.json > {log} 2>&1
		"""

rule chunk_fasta:
	output:
		chunked_fasta = "results2/{sample_id}/{region_id}/shasta/{sample_id}.subregion.fasta"
	input:
		chunked_gaf="results2/{sample_id}/{region_id}/chunk/subgraph.gaf",
		fasta="results/reads/{sample_id}.fasta"
	benchmark:
		"benchmarks/v2/{sample_id}/{region_id}/chunk_fasta.benchmark.txt"
	log:
		"logs/v2/{sample_id}/{region_id}/chunk_fasta.log"
	container:
		"docker://pegi3s/seqkit:latest"
	shell:
		"""
		# Extract READ IDs from chunked GAF and then extract fasta records for these reads
		cut -f1 {input.chunked_gaf} | sort -u -T {config[TMPDIR]}| seqkit grep -f - {input.fasta} > {output.chunked_fasta}
		"""

rule align_reads_to_linear_reference:
	output:
		alignment_bam = "results/alignment_chm13/{sample_id}/{sample_id}_to_CHM13_minimap.bam",
		alignment_bam_idx = "results/alignment_chm13/{sample_id}/{sample_id}_to_CHM13_minimap.bam.bai"
	input:
		fastq="/private/groups/migalab/kkyriaki/experiments/data/GIAB_2025/HG002/{sample_id}/{sample_id}.fastq",
		reference=config["chm13_ref"]
	params:
		mapMode="map-ont",
		kmerSize=config["MINIMAP"]["kmerSize"],
		minibatchSize=config["MINIMAP"]["minibatchSize"],
		mdString=config["MINIMAP"]["mdString"],
		eqxString=config["MINIMAP"]["eqxString"]
	benchmark: "benchmarks/v2/{sample_id}/align_reads_to_linear_reference.benchmark.txt"
	log: "logs/v2/{sample_id}/align_reads_to_linear_reference.log"
	container: "docker://mkolmogo/card_minimap2:2.23"
	threads: 64
	shell:
		"""
		minimap2 -ax {params.mapMode} {input.reference} {input.fastq} -k {params.kmerSize} -y -K {params.minibatchSize} -t {threads} {params.mdString} {params.eqxString} | samtools sort -@16 - > {output.alignment_bam}
		samtools index -@ {threads} {output.alignment_bam}
		"""

# rule call_variants_dv:
# 	output:
# 		vcf = "results/alignment_chm13/{sample_id}/{sample_id}_to_CHM13_dv.vcf.gz"
# 	input:
# 		reference=config["chm13_ref"]
# 		alignment_bam = "results/alignment_chm13/{sample_id}/{sample_id}_to_CHM13_minimap.bam",
# 		alignment_bam_idx = "results/alignment_chm13/{sample_id}/{sample_id}_to_CHM13_minimap.bam.bai"
# 	params:
# 		dvModel="ONT_R104",
# 		extraArguments=""
# 	benchmark: "benchmarks/v2/{sample_id}/call_variants_dv.benchmark.txt"
# 	log: "logs/v2/{sample_id}/call_variants_dv.log"
# 	container: "docker://google/deepvariant:cl508467184"
# 	threads: 64
# 	shell:
# 		"""
# 		/opt/deepvariant/bin/run_deepvariant 
# 		--model_type {params.dvModel} \
#         --ref {reference} \
#         --reads {alignment_bam} \
#         --sample_name {wildcards.sample_id} \
#         --output_vcf {output.vcf} \
#         --num_shards {threads} {params.extraArguments}
# 		"""

rule run_shasta_assembly_without_extention:
	output: "results2/{sample_id}/{region_id}/shasta_noext/ShastaRun/Assembly.fasta"
	input:
		shasta=config["SHASTA"]["bin"],
		chunked_fasta = "results2/{sample_id}/{region_id}/shasta/{sample_id}.subregion.fasta",
		shasta_conf = config["SHASTA"]["conf"],
		anchors="results2/{sample_id}/{region_id}/anchors/subgraph.anchors.json.jsonl"
	benchmark: "benchmarks/v2/{sample_id}/{region_id}/run_shasta_assembly_without_extention.benchmark.txt"
	log: "logs/v2/{sample_id}/{region_id}/run_shasta_assembly_without_extention.log"
	shell:
		"""
		## make a copy of the anchors.json for Shasta
		cp {input.anchors} results2/{wildcards.sample_id}/{wildcards.region_id}/shasta_noext/anchors.json
		rm -rf results2/{wildcards.sample_id}/{wildcards.region_id}/shasta_noext/ShastaRun
		{input.shasta} --input {input.chunked_fasta} --assemblyDirectory results2/{wildcards.sample_id}/{wildcards.region_id}/shasta_noext/ShastaRun --config {input.shasta_conf} --anchors results2/{wildcards.sample_id}/{wildcards.region_id}/shasta_noext/anchors.json --memoryMode filesystem --memoryBacking disk > {log} 2>&1
		"""

rule run_shasta_assembly:
	output: "results2/{sample_id}/{region_id}/shasta/ShastaRun/Assembly.fasta"
	input:
		shasta=config["SHASTA"]["bin"],
		chunked_fasta = "results2/{sample_id}/{region_id}/shasta/{sample_id}.subregion.fasta",
		shasta_conf = config["SHASTA"]["conf"],
		anchors="results2/{sample_id}/{region_id}/anchors/subgraph.anchors.json.extended.jsonl"
	benchmark: "benchmarks/v2/{sample_id}/{region_id}/run_shasta_assembly.benchmark.txt"
	log: "logs/v2/{sample_id}/{region_id}/run_shasta_assembly.log"
	shell:
		"""
		## make a copy of the anchors.json for Shasta
		cp {input.anchors} results2/{wildcards.sample_id}/{wildcards.region_id}/shasta/anchors.json
		rm -rf results2/{wildcards.sample_id}/{wildcards.region_id}/shasta/ShastaRun
		{input.shasta} --input {input.chunked_fasta} --assemblyDirectory results2/{wildcards.sample_id}/{wildcards.region_id}/shasta/ShastaRun --config {input.shasta_conf} --anchors results2/{wildcards.sample_id}/{wildcards.region_id}/shasta/anchors.json --memoryMode filesystem --memoryBacking disk > {log} 2>&1
		"""

rule align_assembly_to_reference:
	output:
		paf="results2/{sample_id}/{region_id}/assembly_alignment/{sample_id}_{region_id}_shasta_to_hg002_minimap_{asm_preset}.paf"
	input:
		analyzePaf_bin=config["ANALYSEPAF"]["bin"],
		hg002_reference=config["HG002v101_ref"],
		subregion_shasta_assembly="results2/{sample_id}/{region_id}/shasta/ShastaRun/Assembly.fasta"
	params:
		asm_preset=config["MINIMAP"]["asmPreset"]
	benchmark: "benchmarks/v2/{sample_id}/{region_id}/align_assembly_to_reference_{asm_preset}.benchmark.txt"
	log: "logs/v2/{sample_id}/{region_id}/align_assembly_to_reference_{asm_preset}.log"
	container: "docker://mkolmogo/card_minimap2:2.23"
	threads: 64
	shell:
		"""
		minimap2 -t {threads} -I 20G -cx {params.asm_preset} -K 0M --eqx --cs {input.hg002_reference} {input.subregion_shasta_assembly} > {output.paf}
		{input.analyzePaf_bin} \
			--input {output.paf} \
			--output results2/{wildcards.sample_id}/{wildcards.region_id}/assembly_alignment/{wildcards.sample_id}_{wildcards.region_id}_shasta_to_hg002_minimap_{wildcards.asm_preset} \
			--pixelsPerMb 5 --minAlignmentQuality 1 --minAlignmentLength 0 > {log} 2>&1
		"""

rule get_anchor_stats:
	output:
		anchor_reads_info="results2/{sample_id}/{region_id}/anchors/anchor_reads_info.tsv",
		anchor_stats_dir=directory("results2/{sample_id}/{region_id}/anchor_stats")
	input:
		python_script="/private/groups/migalab/shnegi/vg_anchors_project/notebooks/python-scripts/old-method-scripts/preprocess_vganchor_outfiles.py",
		R_script="/private/groups/migalab/shnegi/vg_anchors_project/notebooks/python-scripts/old-method-scripts/get_anchor_stats.R",
		anchors="results2/{sample_id}/{region_id}/anchors/subgraph.anchors.json.jsonl",
		subregion_shasta_assembly="results2/{sample_id}/{region_id}/shasta_noext/ShastaRun/Assembly.fasta"
	params:
		seqtech="ont_r10",
		region=config['region']['chromosome'] + ":" + config['region']['start'] + "-" + config['region']['end']
	log: "logs/v2/{sample_id}/{region_id}/get_anchor_stats.log"
	shell:
		"""
		# Since this JSON is confusing to interpret, convert it to a TSV...
		python3 {input.python_script} -j {input.anchors}
		# Next, generate anchor stats plots...
		Rscript {input.R_script} -d results2/{wildcards.sample_id}/{wildcards.region_id} -o {params.seqtech}_{wildcards.region_id} -r {params.region} > {log} 2>&1
		"""

rule get_extended_anchor_stats:
	output:
		anchor_reads_info="results2/{sample_id}/{region_id}/anchors/extended_anchor_reads_info.tsv",
		anchor_stats_dir=directory("results2/{sample_id}/{region_id}/extended_anchor_stats")
	input:
		python_script="/private/groups/migalab/shnegi/vg_anchors_project/notebooks/python-scripts/old-method-scripts/preprocess_vganchor_outfiles_extention.py",
		R_script="/private/groups/migalab/shnegi/vg_anchors_project/notebooks/python-scripts/old-method-scripts/get_extended_anchor_stats.R",
		anchors="results2/{sample_id}/{region_id}/anchors/subgraph.anchors.json.extended.jsonl",
		subregion_shasta_assembly="results2/{sample_id}/{region_id}/shasta/ShastaRun/Assembly.fasta"
	params:
		seqtech="ont_r10",
		region=config['region']['chromosome'] + ":" + config['region']['start'] + "-" + config['region']['end']
	log: "logs/v2/{sample_id}/{region_id}/get_extended_anchor_stats.log"
	shell:
		"""
		# Since this JSON is confusing to interpret, convert it to a TSV...
		python3 {input.python_script} -j {input.anchors}
		# Next, generate anchor stats plots...
		Rscript {input.R_script} -d results2/{wildcards.sample_id}/{wildcards.region_id} -o {params.seqtech}_{wildcards.region_id} -r {params.region} > {log} 2>&1
		"""

rule chunk_hg002_reference:
	output:
		chunked_reference="results2/{sample_id}/{region_id}/assembly_alignment/hg002.chunked.fasta"
	input:
		hg002_reference=config["HG002v101_ref"]
	params:
		mat_range=config['region']['chromosome'] + "_MATERNAL:" + str(int(config['region']['start']) - config['region']['flank']) + "-" + str(int(config['region']['end']) + config['region']['flank']),
		pat_range=config['region']['chromosome'] + "_PATERNAL:" + str(int(config['region']['start']) - config['region']['flank']) + "-" + str(int(config['region']['end']) + config['region']['flank'])
	benchmark:
		"benchmarks/v2/{sample_id}/{region_id}/chunk_hg002_reference.benchmark.txt"
	log:
		"logs/v2/{sample_id}/{region_id}/chunk_hg002_reference.log"
	shell:
		"""
		echo {params.mat_range} {params.pat_range} > {log} 2>&1
		samtools faidx {input.hg002_reference} {params.mat_range} {params.pat_range} > {output.chunked_reference}
		"""

rule align_assembly_to_chunked_reference:
	output:
		paf="results2/{sample_id}/{region_id}/assembly_alignment/{sample_id}_{region_id}_ZOOMED_shasta_to_hg002_minimap_{asm_preset}.paf"
	input:
		analyzePaf_bin=config["ANALYSEPAF"]["bin"],
		hg002_reference_chunked="results2/{sample_id}/{region_id}/assembly_alignment/hg002.chunked.fasta",
		subregion_shasta_assembly="results2/{sample_id}/{region_id}/shasta/ShastaRun/Assembly.fasta"
	params:
		asm_preset=config["MINIMAP"]["asmPreset"]
	benchmark: "benchmarks/v2/{sample_id}/{region_id}/align_assembly_to_reference_{asm_preset}.benchmark.txt"
	log: "logs/v2/{sample_id}/{region_id}/align_assembly_to_reference_{asm_preset}.log"
	container: "docker://mkolmogo/card_minimap2:2.23"
	threads: 64
	shell:
		"""
		minimap2 -t {threads} -I 20G -cx {params.asm_preset} -K 1M --eqx --cs {input.hg002_reference_chunked} {input.subregion_shasta_assembly} > {output.paf}
		{input.analyzePaf_bin} \
			--input {output.paf} \
			--output results2/{wildcards.sample_id}/{wildcards.region_id}/assembly_alignment/{wildcards.sample_id}_{wildcards.region_id}_ZOOMED_shasta_to_hg002_minimap_{wildcards.asm_preset} \
			--pixelsPerMb 5 --minAlignmentQuality 0 > {log} 2>&1
		"""

rule run_displayPafAlignments:
	output:
		csv="results2/{sample_id}/{region_id}/assembly_alignment/{sample_id}_{region_id}_ZOOMED_shasta_to_hg002_minimap_{asm_preset}_displayPaf.csv",
		plots_dir=directory("results2/{sample_id}/{region_id}/assembly_alignment/{asm_preset}_plots")
	input:
		displayPaf_bin=config["DISPLAYPAF"]["bin"],
		hg002_reference_chunked="results2/{sample_id}/{region_id}/assembly_alignment/hg002.chunked.fasta",
		subregion_shasta_assembly="results2/{sample_id}/{region_id}/shasta/ShastaRun/Assembly.fasta",
		paf="results2/{sample_id}/{region_id}/assembly_alignment/{sample_id}_{region_id}_ZOOMED_shasta_to_hg002_minimap_{asm_preset}.paf",
		R_script="/private/groups/migalab/shnegi/vg_anchors_project/notebooks/python-scripts/old-method-scripts/analyse_displayPaf_outputs.R"
	params:
		asm_preset=config["MINIMAP"]["asmPreset"],
	benchmark: "benchmarks/v2/{sample_id}/{region_id}/run_displayPafAlignments_{asm_preset}.benchmark.txt"
	log: "logs/v2/{sample_id}/{region_id}/run_displayPafAlignments_{asm_preset}.log"
	shell:
		"""
		{input.displayPaf_bin} \
			--paf {input.paf} -r {input.hg002_reference_chunked} -a {input.subregion_shasta_assembly} \
			--output results2/{wildcards.sample_id}/{wildcards.region_id}/assembly_alignment/{wildcards.sample_id}_{wildcards.region_id}_ZOOMED_shasta_to_hg002_minimap_{wildcards.asm_preset}_displayPaf \
			--minAlignmentQuality 0 --minAlignmentLength 0 > {log} 2>&1
		
		Rscript {input.R_script} -c {output.csv} -o {wildcards.sample_id}_{wildcards.region_id} >> {log} 2>&1
		"""
